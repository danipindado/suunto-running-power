/* While in sport mode do this once per second */
/*before start: configure parameters, initialize*/
if(SUUNTO_DURATION<1)
{
  initialSpeed = SUUNTO_SPEED/3.6; 
  RESULT=0;
}
else 
{

  /* slope */
  speed = SUUNTO_SPEED/3.6;
  if(SUUNTO_SPEED < 1)
  {
  slope = 0.0;
  }
  else 
  {
    slope = 0.06 * SUUNTO_VERTICAL_SPD / SUUNTO_SPEED;
  }

  /* 2D speed */
  speed = SUUNTO_SPEED/3.6;
  
  /* 3D speed */
  speed = speed/Suunto.cos(Suunto.atan2(slope,1));

  /*Aero contribution (Arsac 2001): 0.5 * rho * Cd * Af * V^2, rho = 1.2, Cd = 0.9*/
  Af = (0.2025*Suunto.pow(SUUNTO_USER_HEIGHT/100, 0.725)*Suunto.pow(SUUNTO_USER_WEIGHT, 0.425))*0.266; /* Frontal Area*/
  cAero = 0.5*1.2*0.9*Af*Suunto.pow(speed, 2);

  /* Kinetic Energy contribution (Arsac 2001): 0.5 * (V^2-V0^2) / d*/
  if(SUUNTO_DISTANCE >0)
  {
    cKin = 0.5*(Suunto.pow(speed,2)-Suunto.pow(initialSpeed,2))/(1000*SUUNTO_DISTANCE);
  }
  else
  {
    cKin = 0.0;
  }


  /* Energy Cost of Running according to slope (Minetti 2002)*/
  /*cSlope = 155.4*Suunto.pow(slope,5) - 30.4*Suunto.pow(slope,4) - 43.3*Suunto.pow(slope,3) + 46.3*Suunto.pow(slope,2) + 19.5*slope + 3.6;*/
  cSlope = ((((155.4*slope-30.4)*slope-43.3)*slope+46.3)*slope+19.5)*slope + 3.6;

  /* Efficiency (Skiba's govss.pdf and spreadsheet)*/
  /* eff = (0.25 + 0.054*speed)*(1 - 0.5*speed/8.33);*/
  eff = (-0.0032413*speed+0.038994)*speed+0.25;

  power = (cAero + cKin + cSlope*eff*SUUNTO_USER_WEIGHT)*speed;
  if(power <0)
  {
    power =0;
  }
  RESULT=power;
}